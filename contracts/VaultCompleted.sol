// SPDX-License-Identifier: MIT
pragma solidity ^0.8.26;

contract Vault {
    IERC20 public immutable token;

    uint256 public totalSupply;
    mapping(address => uint256) public balanceOf;

    address public admin;
    uint256 public feePercent; // 2 means 2%

    // Governance Membership data structures
    uint256 private _membershipTokenId;
    mapping(uint256 => address) public _ownerOfmembershipToken;
    mapping(address => uint256) public _balanceOfmembershipToken;
    mapping(address => uint256) public _membershipTokenOf;

    function _mintMembership(address to) internal {
        if (_balanceOfmembershipToken[to] == 0) {
            _membershipTokenId++;
            uint256 newId = _membershipTokenId;

            _ownerOfmembershipToken[newId] = to;
            _balanceOfmembershipToken[to] = 1;
            _membershipTokenOf[to] = newId;
        }
    }

    function _burnMembership(address from) internal {
        uint256 tokenId = _membershipTokenOf[from];
        if (tokenId != 0) {
            delete _ownerOfmembershipToken[tokenId];
            delete _membershipTokenOf[from];
            delete _balanceOfmembershipToken[from];
        }
    }

    constructor(address _token, address _admin, uint256 _feePercent) {
        token = IERC20(_token);
        admin = _admin;
        feePercent = _feePercent;
    }

    function _mint(address _to, uint256 _shares) private {
        totalSupply += _shares;
        balanceOf[_to] += _shares;

        _mintMembership(_to);
    }

    function _burn(address _from, uint256 _shares) private {
        totalSupply -= _shares;
        balanceOf[_from] -= _shares;
    }

    function deposit(uint256 _amount) external {
        require(_amount > 0, "amount = 0");

        uint256 shares;
        if (totalSupply == 0) {
            shares = _amount;
        } else {
            shares = (_amount * totalSupply) / token.balanceOf(address(this));
        }

        _mint(msg.sender, shares);

        require(
            token.transferFrom(msg.sender, address(this), _amount),
            "transferFrom failed"
        );
    }

    function withdraw(uint256 _shares) external {
        require(_shares > 0, "shares = 0");
        require(balanceOf[msg.sender] >= _shares, "not enough shares");
        require(totalSupply > 0, "no shares exist");

        uint256 grossAmount =
            (_shares * token.balanceOf(address(this))) / totalSupply;

        _burn(msg.sender, _shares);

        uint256 fee = (grossAmount * feePercent) / 100;
        uint256 amount = grossAmount - fee;

        require(token.transfer(msg.sender, amount), "user transfer failed");
        require(token.transfer(admin, fee), "admin fee transfer failed");

        if (balanceOf[msg.sender] == 0) {
            _burnMembership(msg.sender);
        }
    }
}

interface IERC20 {
    function totalSupply() external view returns (uint256);
    function balanceOf(address account) external view returns (uint256);
    function transfer(address recipient, uint256 amount)
        external
        returns (bool);
    function allowance(address owner, address spender)
        external
        view
        returns (uint256);
    function approve(address spender, uint256 amount)
        external
        returns (bool);
    function transferFrom(address sender, address recipient, uint256 amount)
        external
        returns (bool);
}
